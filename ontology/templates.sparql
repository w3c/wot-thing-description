prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix schema: <http://schema.org/>
prefix vann: <http://purl.org/vocab/vann/>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/ontology#>

template :main(?prefix) {
    # TODO allow arbitrary format expression instead (requires extending STTL.js)
    if(?prefix = "td", st:call-template(:main-td, ?prefix),
    if(?prefix = "jsonschema", st:call-template(:main-jsonschema, ?prefix),
    if(?prefix = "wotsec", st:call-template(:main-wotsec, ?prefix),
    if(?prefix = "hctl", st:call-template(:main-hctl, ?prefix), ""))))
} where {}

template :main-td(?prefix) {
    format {
        <file://./ontology/td.template.html>
        st:call-template(:axioms, ?prefix)
    }
} where {}

template :main-jsonschema(?prefix) {
    format {
        <file://./ontology/jsonschema.template.html>
        st:call-template(:axioms, ?prefix)
    }
} where {}

template :main-wotsec(?prefix) {
    format {
        <file://./ontology/wotsec.template.html>
        st:call-template(:axioms, ?prefix)
    }
} where {}

template :main-hctl(?prefix) {
    format {
        <file://./ontology/hctl.template.html>
        st:call-template(:axioms, ?prefix)
    }
} where {}

template :axioms(?prefix) {
    "<section>"
        "<h2>Axiomatization</h2>"
        "<section>"
            "<h3>Classes</h3>"
            st:call-template(:classes, ?ns, ?prefix)
        "</section>"
        "<section>"
            "<h3>Object Properties</h3>"
            st:call-template(:properties-intro, ?ns, ?prefix, owl:ObjectProperty)
            st:call-template(:properties, ?ns, ?prefix, owl:ObjectProperty)
        "</section>"
        "<section>"
            "<h3>Datatype Properties</h3>"
            st:call-template(:properties-intro, ?ns, ?prefix, owl:DatatypeProperty)
            st:call-template(:properties, ?ns, ?prefix, owl:DatatypeProperty)
        "</section>"
        "<section>"
            "<h3>Annotation Properties</h3>"
            st:call-template(:properties-intro, ?ns, ?prefix, owl:AnnotationProperty)
            st:call-template(:properties, ?ns, ?prefix, owl:AnnotationProperty)
        "</section>"
        st:call-template(:individuals-section, ?ns, ?prefix)
    "</section>"
} where {
    ?o a owl:Ontology ;
       vann:preferredNamespaceUri ?ns ;
       vann:preferredNamespacePrefix ?prefix .
}

template :individuals-section(?ns ?prefix) {
    "<section>"
        "<h3>Named Individuals</h3>"
        st:call-template(:individuals, ?ns, ?prefix)
    "</section>"
} where {
    ?o a owl:Ontology ;
       vann:preferredNamespacePrefix ?prefix .
    filter exists {
        ?i a ?c ;
           rdfs:isDefinedBy ?o .
        ?c rdfs:subClassOf schema:Enumeration .
    }
}

template :classes(?ns ?prefix) {
    format { "<section id=\"%s\">" ?label }
        st:call-template(:term-header, ?c, ?prefix)
        "<table class=\"def numbered\">"
            "<tbody>"
                st:call-template(:class-superclass, ?c, ?prefix)
                st:call-template(:class-subclasses, ?c, ?prefix)
                st:call-template(:class-domain-all, ?c, ?prefix)
                st:call-template(:class-range-all, ?c, ?prefix)
                st:call-template(:enumeration-members, ?c, ?prefix)
            "</tbody>"
        "</table>"
    "</section>"
} where {
    ?c a owl:Class ;
       rdfs:comment ?desc .
    filter (strstarts(str(?c), str(?ns)))
    bind (strafter(str(?c), "#") as ?label)
} order by ?c

template :class-superclass(?c ?prefix) {
    "<tr>"
        "<td>Sub-class of</td>"
        format {
            "<td>%s</td>"
            st:call-template(:term-link, ?sc, ?prefix)
        }
    "</tr>"
} where {
    ?c rdfs:subClassOf ?sc .
    ?sc a owl:Class .
}

template :class-subclasses(?c ?prefix) {
    "<tr>"
        "<td>Super-class of</td>"
        format {
            "<td>%s</td>"
            st:call-template(:class-subclass, ?c, ?prefix)
        }
    "</tr>"
} where {
    filter exists {
        ?sc rdfs:subClassOf ?c .
        ?sc a owl:Class .
    }
}

template :class-subclass(?c ?prefix) {
    st:call-template(:term-link, ?sc, ?prefix) ; separator = "<br>"
} where {
    ?sc rdfs:subClassOf ?c .
    ?sc a owl:Class .
} order by ?sc

template :class-domain-all(?c ?prefix) {
    "<tr>"
        "<td>In the domainÂ of</td>"
        "<td>" st:call-template(:class-domain, ?c, ?prefix) "</td>"
    "</tr>"
} where {
    filter exists {
        ?p schema:domainIncludes ?c .
    }
}

template :class-domain(?c ?prefix) {
    st:call-template(:term-link, ?p, ?prefix) ; separator = "<br>"
} where {
    ?p schema:domainIncludes ?c .
} order by ?p

template :class-range-all(?c ?prefix) {
    "<tr>"
        "<td>In the range of</td>"
        "<td>" st:call-template(:class-range, ?c, ?prefix) "</td>"
    "</tr>"
} where {
    filter exists {
        ?p schema:rangeIncludes ?c .
    }
}

template :class-range(?c ?prefix) {
    st:call-template(:term-link, ?p, ?prefix) ; separator = "<br>"
} where {
    ?p schema:rangeIncludes ?c .
} order by ?p

template :enumeration-members(?c ?prefix) {
    "<tr>"
        "<td>Enumeration members</td>"
        "<td>" st:call-template(:enumeration-member, ?c, ?prefix) "</td>"
    "</tr>"
} where {
    ?c rdfs:subClassOf schema:Enumeration .
}

template :enumeration-member(?c ?prefix) {
    st:call-template(:term-link, ?member, ?prefix) ; separator = "<br>"
} where {
    ?c rdfs:subClassOf schema:Enumeration .
    ?member a ?c .
} order by ?member

template :properties(?ns ?prefix ?type) {
    format { "<section id=\"%s\">" ?label }
        st:call-template(:term-header, ?p, ?prefix)
        "<table class=\"def numbered\">"
            "<tbody>"
                st:call-template(:property-domain-all, ?p, ?prefix)
                st:call-template(:property-range-all, ?p, ?prefix)
            "</tbody>"
        "</table>"
    "</section>"
} where {
    ?p a ?type ;
       rdfs:comment ?desc .
    filter (strstarts(str(?p), str(?ns)))
    bind (strafter(str(?p), "#") as ?label)
} order by ?p

template :properties-intro(?ns ?prefix ?type) {
    format { "%s" if(?found, "", concat("<p>No ", strafter(str(?type), "#"), " found in the ontology.</p>")) }
} where {
    optional {
        ?p a ?type .
        filter (strstarts(str(?p), str(?ns)))
        bind (strafter(str(?p), "#") as ?label)
    }
    bind(bound(?p) as ?found)
} order by ?p

template :property-domain-all(?p ?prefix) {
    "<tr>"
        "<td>Domain includes</td>"
        format {
            "<td>%s</td>"
            st:call-template(:property-domain, ?p, ?prefix)
        }
    "</tr>"
} where {
    filter exists {
        ?p schema:domainIncludes ?dom .
    }
}

template :property-domain(?p ?prefix) {
    st:call-template(:term-link, ?dom, ?prefix) ; separator = "<br>"
} where {
    { ?p rdfs:domain ?dom . }
    union
    { ?p schema:domainIncludes ?dom . }
    filter (isIRI(?dom))
} order by ?dom

template :property-range-all(?p ?prefix) {
    "<tr>"
        "<td>Range includes</td>"
        format {
            "<td>%s</td>"
            st:call-template(:property-range, ?p, ?prefix)
        }
    "</tr>"
} where {
    filter exists {
        ?p schema:rangeIncludes ?range .
    }
}

template :property-range(?p ?prefix) {
    st:call-template(:term-link, ?range, ?prefix) ; separator = "<br>"
} where {
    { ?p rdfs:range ?range . }
    union
    { ?p schema:rangeIncludes ?range . }
    filter (isIRI(?range))
} order by ?range

template :individuals(?ns ?prefix) {
    format { "<section id=\"%s\">" ?label }
        st:call-template(:term-header, ?i, ?prefix)
        "<table class=\"def numbered\">"
            "<tbody>"
                "<tr>"
                    "<td>Instance of</td>"
                    "<td>" st:call-template(:term-link, ?c, ?prefix) "</td>"
                "</tr>"
            "</tbody>"
        "</table>"
    "</section>"
} where {
    ?i a ?c .
    ?c rdfs:subClassOf schema:Enumeration .
    filter (strstarts(str(?i), str(?ns)))
    bind (strafter(str(?i), "#") as ?label)
}

template :term-header(?term ?prefix) {
    format { "<h4>%s</h4>" ?label }
    format { "<p>IRI: <code>%s</code></p>" ?term }
    format { "<span>%s</span>" ?desc }
} where {
    ?term rdfs:comment ?desc
    optional { ?term rdfs:label ?l }
    bind (if(bound(?l), ?l, strafter(str(?term), "#")) as ?label)
}

template :term-link(?term ?defaultPrefix) {
    format {
        "<code><a href=\"%s\">%s</a></code>"
        if(?prefix = ?defaultPrefix,
            concat("#", ?label),
            str(?term))
        if(?curie, concat(?prefix, ":", ?label), ?label)
    }
} where {
    {
        ?o a owl:Ontology .
        filter (?o != schema:)
        optional { ?o vann:preferredNamespacePrefix ?prefix }
    }
    union
    {
        bind (schema: as ?o)
        bind ("schema" as ?prefix)
    }
    filter (strstarts(str(?term), str(?o)))
    bind (bound(?prefix) as ?curie)
    optional { ?term rdfs:label ?l }
    bind (if(bound(?l), ?l, strafter(str(?term), str(?o))) as ?label)
}