prefix st: <http://ns.inria.fr/sparql-template/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix jsonld: <http://www.w3.org/ns/json-ld#>
prefix vann: <http://purl.org/vocab/vann/>
prefix : <http://w3c.github.io/wot-thing-description/visualization#>

template :main(?prefix) {
    "digraph {"
        st:call-template(:classes, ?ns)
        st:call-template(:associations, ?ns)
        st:call-template(:subClassRelations, ?ns)
    "}"
} where {
    ?o a owl:Ontology ;
       vann:preferredNamespaceUri ?ns ;
       vann:preferredNamespacePrefix ?prefix .
}

template :classes(?ns) {
    ?name " [shape=none,label=<"
        "<table border=\"0\" cellborder=\"1\" cellspacing=\"0\" color=\"#8ccbf2\">"
            "<tr><td bgcolor=\"#8ccbf2\">"
                format { "<b><font color=\"#c83500\" face=\"mono\">%s</font></b>" ?name }
            "</td></tr>"
            # see also ../templates.sparql (custom id property)
            if(?class = <https://www.w3.org/2019/wot/td#Thing>,
            concat("<tr><td align=\"left\" bgcolor=\"#ddeeff\">",
                "<font color=\"#c83500\" face=\"mono\">@context</font>",
                " : ",
                "<i><font color=\"#c83500\" face=\"mono\">anyURI</font> or Array</i>",
                " <i>(mandatory)</i>",
                "</td></tr>",
                "<tr><td align=\"left\" bgcolor=\"#ddeeff\">",
                "<font color=\"#c83500\" face=\"mono\">@type</font>",
                " : ",
                "<i>(Array of) </i><font color=\"#c83500\" face=\"mono\">string</font>",
                "</td></tr>",
                "<tr><td align=\"left\" bgcolor=\"#ddeeff\">",
                "<font color=\"#c83500\" face=\"mono\">id</font>",
                " : ",
                "<font color=\"#c83500\" face=\"mono\">anyURI</font>",
                " <i>(mandatory)</i>",
                "</td></tr>"
            ), "")
            if(?class = <https://www.w3.org/2019/wot/td#InteractionPattern>,
            concat("<tr><td align=\"left\" bgcolor=\"#ddeeff\">",
                "<font color=\"#c83500\" face=\"mono\">@type</font>",
                " : ",
                "<i>(Array of) </i><font color=\"#c83500\" face=\"mono\">string</font>",
                "</td></tr>"
            ), "")
            if(?class = <https://www.w3.org/2019/wot/json-schema#DataSchema>,
            concat(
                "<tr><td align=\"left\" bgcolor=\"#ddeeff\">",
                "<font color=\"#c83500\" face=\"mono\">@type</font>",
                " : ",
                "<i>(Array of) </i><font color=\"#c83500\" face=\"mono\">string</font>",
                "</td></tr>"
            ), "")
            if(?class = <https://www.w3.org/2019/wot/security#SecurityScheme>,
            concat(
                "<tr><td align=\"left\" bgcolor=\"#ddeeff\">",
                "<font color=\"#c83500\" face=\"mono\">@type</font>",
                " : ",
                "<i>(Array of) </i><font color=\"#c83500\" face=\"mono\">string</font>",
                "</td></tr>"
            ), "")
            st:call-template(:attributes, ?class, ?ns)
        "</table>"
    ">];"
} where {
    ?shape a sh:NodeShape ;
           sh:targetClass ?class .
    filter(strstarts(str(?class), str(?ns)))
    bind (strafter(str(?class), "#") as ?name)
}

template :attributes(?class ?ns) {
    "<tr><td align=\"left\" bgcolor=\"#ddeeff\">"
        st:call-template(:attribute, ?class, ?prop, ?ns)
    "</td></tr>" ; separator = ""
} where {
    ?shape a sh:NodeShape ;
           sh:targetClass ?class ;
           sh:property ?propShape .
    ?propShape sh:path ?prop ;
               sh:datatype ?datatype
}

template :attribute(?class ?prop ?ns) {
    format {
        "<font color=\"#c83500\" face=\"mono\">%s</font>"
        st:call-template(:numbered-label, ?prop, ?ns, ?singleton)
    }
    " : "
    format {
        "%s<font color=\"#c83500\" face=\"mono\">%s</font>"
        if(?array, "Array of ",
        if(?map, "Map of ",
        if(!?singleton, "(Array of) ", "")))
        st:call-template(:label, ?datatype)
    }
    format { "%s" if(?mandatory, " <i>(mandatory)</i>", "") }
} where {
    ?shape a sh:NodeShape ;
           sh:targetClass ?class ;
           sh:property ?propShape .
    ?propShape sh:path ?prop ;
               sh:datatype ?datatype ;
    optional { ?propShape sh:minCount ?minCount }
    optional { ?propShape sh:maxCount ?maxCount }
    optional {
        ?mSet a jsonld:Mapping ;
           jsonld:iri ?prop ;
           jsonld:container jsonld:set
    }
    optional {
        ?mIndex a jsonld:Mapping ;
        jsonld:iri ?prop .
        { ?mIndex jsonld:container jsonld:index }
        union
        { ?mIndex jsonld:container jsonld:language }
    }
    bind (bound(?minCount) as ?mandatory)
    bind (bound(?maxCount) as ?singleton)
    bind (bound(?mSet) as ?array)
    bind (bound(?mIndex) as ?map)
    bind (bound(?def) as ?hasDef)
}

template :associations(?ns) {
    ?source " -> " ?dest " [label=<"
        "<font color=\"#c83500\" face=\"mono\">"
            format { "%s" st:call-template(:label, ?prop) }
        "</font>"
    ">,headlabel="
        format {
            "\"%s..%s\""
            ?min
            if(?prop = <https://www.w3.org/2019/wot/json-schema#items>, "*", ?max)
        }
    "];"
    (if (?ext, st:call-template(:externalClass, ?dest), ""))
} where {
    ?sourceShape a sh:NodeShape ;
           sh:targetClass ?sourceClass ;
           sh:property ?propShape .
    ?propShape sh:path ?prop ;
               sh:node ?destShape .
    ?destShape a sh:NodeShape ;
               sh:targetClass ?destClass .
    optional { ?propShape sh:minCount ?minCount }
    optional { ?propShape sh:maxCount ?maxCount }
    optional {
        ?m a jsonld:Mapping ;
           jsonld:iri ?prop ;
           jsonld:container jsonld:set
    }
    filter (isIRI(?sourceClass) && isIRI(?destClass) && isIRI(?prop))
    filter (strstarts(str(?sourceClass), str(?ns)))
    bind (strafter(str(?sourceClass), "#") as ?source)
    bind (strafter(str(?destClass), "#") as ?dest)
    bind (!strstarts(str(?destClass), str(?ns)) as ?ext)
    bind (if(bound(?minCount), ?minCount, "0") as ?min)
    bind (if(bound(?maxCount), ?maxCount, "*") as ?max)
}

# TODO one single template for local and external classes
template :externalClass(?label) {
    ?label " [shape=none,label=<"
        "<font face=\"mono\">"
            "<table border=\"0\" cellborder=\"1\" cellspacing=\"0\" color=\"#8c8c8c\">"
                format { "<tr><td bgcolor=\"#f2f2f2\"><b>%s</b></td></tr>" ?label }
            "</table>"
        "</font>"
    ">]"
} where {}

template :subClassRelations(?ns) {
    ?source " -> " ?dest " [fillcolor=white];"
} where {
    ?sourceClass rdfs:subClassOf ?destClass .
    ?sourceShape a sh:NodeShape ;
           sh:targetClass ?sourceClass .
    ?destShape a sh:NodeShape ;
           sh:targetClass ?destClass .
    filter (isIRI(?sourceClass) && isIRI(?destClass))
    filter(strstarts(str(?sourceClass), str(?ns)))
    bind (strafter(str(?sourceClass), "#") as ?source)
    bind (strafter(str(?destClass), "#") as ?dest)
}

template :numbered-label(?term ?ns ?singular) {
    format { "%s" ?name }
} where {
    optional {
        ?ctx jsonld:definition ?m1 ;
             jsonld:vocab ?ns .
        ?m1 a jsonld:Mapping ;
            jsonld:iri ?term ;
            jsonld:term ?singularName .
        filter not exists {
            ?m1 jsonld:container ?container
        }
    }
    optional {
        ?ctx jsonld:definition ?m2 ;
                jsonld:vocab ?ns .
        ?m2 a jsonld:Mapping ;
            jsonld:iri ?term ;
            jsonld:term ?pluralName ;
            jsonld:container ?container .
    }
    bind (
        if(!bound(?singularName) && !bound(?pluralName), "",
        if(!bound(?singularName), ?pluralName,
        if(!bound(?pluralName), ?singularName,
        if(?singular, ?singularName, ?pluralName))))
        as ?name
    )
}

template :label(?term) {
    format { "%s" ?label }
} where {
    optional {
        ?m a jsonld:Mapping ;
            jsonld:iri ?term ;
            jsonld:term ?name .
        filter not exists { ?term a owl:Class }
    }
    bind (if(bound(?name), ?name, strafter(str(?term), "#")) as ?label)
}