prefix st: <http://ns.inria.fr/sparql-template/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :classes(?ns) {
    "<section>"
        format { "<h3><code>%s</code></h3>" st:call-template(:label, ?class) }
        format { "<p>%s</p>" st:call-template(:desc, ?class) }

        format { "%s" st:call-template(:fields, ?class) }

        format { "%s" st:call-template(:subclasses, ?class) }

        format { "%s" st:call-template(:notes, ?class) }
    "</section>"
} where {
    ?shape a sh:NodeShape ;
           sh:targetClass ?class .
    filter(strstarts(str(?class), str(?ns)))
    optional { ?shape sh:order ?predefined }
    bind (if(bound(?predefined), ?predefined, "1000"^^xsd:integer) as ?rank)
} order by ?rank

---

prefix st: <http://ns.inria.fr/sparql-template/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix jsonld: <http://www.w3.org/ns/json-ld#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :fields(?class) {
    "<table class=\"def\">"
        "<thead>"
            "<tr>"
                "<th><a>Vocabulary term</a></th>"
                "<th>Description</th>"
                "<th>Mandatory</th>"
                "<th>Type</th>"
            "</tr>"
        "</thead>"
        format {
            "<tbody>%s%s%s%s%s</tbody>"
            # custom property for Thing (id = @id = IRI)
            if(?class = <http://www.w3.org/ns/td#Thing>,
                concat("<tr>",
                       "<td><code>@context</code></td>",
                       "<td>Known from JSON-LD, <code>@context</code> is used to define short-hand names called terms that are used throughout a TD document.<br/><br/><span class=\"rfc2119-assertion\" id=\"td-context-ns-thing\">The first item in the JSON-LD context array MUST be the namespace <code>\"http://www.w3.org/ns/td\"</code></span>, optionally followed by one or more local namespaces. <span class=\"rfc2119-assertion\" id=\"td-context-ns-thing-prefix\">Each optional namespace specifed for a Thing context MUST be bound to an unique prefix.</span></td>",
                       "<td>yes</td>",
                       "<td>array of JSON-LD contexts</td>",
                       "</tr>",
                       "<tr>",
                       "<td><code>@type</code></td>",
                       "<td>Known from JSON-LD, <code>@type</code> is used to state that a thing resource is an instance of a class. One or more classes can be specified.<br/><br/> Each string in the value array represents a class.</td>",
                       "<td>no</td>",
                       "<td>array of <a href=\"http://www.w3.org/TR/2012/REC-xmlschema11-2-20120405/#string\"><code>string</code></a></td>",
                       "</tr>",
                       "<tr>",
                       "<td><code>id</code></td>",
                       "<td>unique identifier of the Thing (URI, e.g. custom URN).</td>",
                       "<td>yes</td>",
                       "<td><a href=\"http://www.w3.org/TR/2012/REC-xmlschema11-2-20120405/#anyURI\"><code>anyURI</code></a></td>",
                       "</tr>"
                ), "")
            if(?class = <http://www.w3.org/ns/td#InteractionPattern>,
                concat("<tr>",
                       "<td><code>@type</code></td>",
                       "<td>Known from JSON-LD, <code>@type</code> is used to state that an interaction resource is an instance of a class. One or more classes can be specified.<br/><br/> Each string in the value array represents a class.</td>",
                       "<td>no</td>",
                       "<td>array of <a href=\"http://www.w3.org/TR/2012/REC-xmlschema11-2-20120405/#string\"><code>string</code></a></td>",
                       "</tr>"
                ), "")
            if(?class = <http://www.w3.org/ns/json-schema#DataSchema>,
                concat("<tr>",
                       "<td><code>@context</code></td>",
                       "<td>Known from JSON-LD, <code>@context</code> is used to define short-hand names called terms that are used in a DataSchema.<br/><br/><span class=\"rfc2119-assertion\" id=\"td-context-ns-dataschema\">The first item in the JSON-LD context array MUST be the namespace <code>\"https://www.w3.org/ns/json-schema\"</code></span>, optionally followed by one or more local namespaces. <span class=\"rfc2119-assertion\" id=\"td-context-ns-dataschema-prefix\">Each optional namespace specified for a DataSchema context MUST be bound to an unique prefix.</span></td>",
                       "<td>yes</td>",
                       "<td>array of JSON-LD contexts</td>",
                       "</tr>",
                       "<tr>",
                       "<td><code>@type</code></td>",
                       "<td>Known from JSON-LD, <code>@type</code> is used to state that values described by the DataSchema are instances of a class. One or more classes can be specified.<br/><br/> Each string in the value array represents a class.</td>",
                       "<td>no</td>",
                       "<td>array of <a href=\"http://www.w3.org/TR/2012/REC-xmlschema11-2-20120405/#string\"><code>string</code></a></td>",
                       "</tr>"
                ), "")
            if(?class = <http://www.w3.org/ns/wot-security#SecurityScheme>,
                concat("<tr>",
                       "<td><code>@context</code></td>",
                       "<td>Known from JSON-LD, <code>@context</code> is used to define short-hand names called terms that are used in a SecurityScheme.<br/><br/><span class=\"rfc2119-assertion\" id=\"td-context-ns-securityscheme\">The first item in the JSON-LD context array MUST be the namespace <code>\"https://www.w3.org/ns/wot-security\"</code></span>, optionally followed by one or more local namespaces. <span class=\"rfc2119-assertion\" id=\"td-context-ns-securityscheme-prefix\">Each optional namespace specified for a SecurityScheme context MUST be bound to an unique prefix.</span></td>",
                       "<td>yes</td>",
                       "<td>array of JSON-LD contexts</td>",
                       "</tr>",
                       "<tr>",
                       "<td><code>@type</code></td>",
                       "<td>Known from JSON-LD, <code>@type</code> is used to state that a security mechanism configured by the SecurityScheme is an instance of a class. One or more classes can be specified.<br/><br/> Each string in the value array represents a class.</td>",
                       "<td>no</td>",
                       "<td>array of <a href=\"http://www.w3.org/TR/2012/REC-xmlschema11-2-20120405/#string\"><code>string</code></a></td>",
                       "</tr>"
                ), "")
            st:call-template(:field, ?class)
        }
    "</table>"
} where {
    ?shape a sh:NodeShape ;
           sh:targetClass ?class .
    filter exists {
        ?shape sh:property ?propShape .
        ?propShape sh:path ?prop .
    }
}

---

prefix st: <http://ns.inria.fr/sparql-template/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix jsonld: <http://www.w3.org/ns/json-ld#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :field(?class) {
    format { "<tr class=\"rfc2119-table-assertion\" id=\"td-vocab-%s\">" st:call-template(:label, ?prop) }
        "<td>"
            format { "<code>%s</code>" st:call-template(:label, ?prop) }
        "</td>"
        "<td>"
            format { "%s" st:call-template(:desc, ?prop) }
            if (?prop = <http://www.w3.org/ns/td#forms>,
                if(?class = <http://www.w3.org/ns/td#Thing>,
                  " In this version of TD, all operations that can be described at the Thing level are concerning how to interact with the Thing's <a href=\"#property\">Property</a> interaction resources collectively at once.", ""
                ) , ""
            )
        "</td>"
        "<td>"
            format { "%s" if(?mandatory, "yes", "no") }
        "</td>"
        "<td>"
            format { 
               "%s%s%s"
               if(?prop = <http://www.w3.org/ns/td#properties>
                ||?prop = <http://www.w3.org/ns/td#actions>
                ||?prop = <http://www.w3.org/ns/td#events>
                ||?prop = <http://www.w3.org/ns/td#uriVariables>
                ||?prop = <http://www.w3.org/ns/td#securityDefinitions>
                ||?prop = <http://www.w3.org/ns/json-schema#properties>,
                  "map of ", 
                  if(?prop = <http://www.w3.org/ns/td#op>,
                      "array of ", # string or array of strings
                      if(?array, "array of ", "")
                  )
               )
               if(?hasType, st:call-template(:type, ?class, ?prop), "any type")
               if(?prop = <http://www.w3.org/ns/td#op>,
                  "  Each entry representing an interaction verb", "")
            }
            format {
                "%s%s%s"
                if(?hasEnum, " (", "")
                st:call-template(:enum, ?class, ?prop)
                if(?hasEnum, ")", "")
            }
        "</td>"
    "</tr>"
} where {
    ?shape a sh:NodeShape ;
        sh:targetClass ?class ;
        sh:property ?propShape .
    ?propShape sh:path ?prop .
    optional { ?propShape sh:datatype|sh:class ?type }
    optional { ?propShape sh:minCount ?count }
    optional {
        ?m a jsonld:Mapping ;
        jsonld:iri ?prop ;
        jsonld:container jsonld:set
    }
    optional {
        ?propShape sh:in ?list
    }
    optional { ?propShape sh:defaultValue ?def  }
    bind (bound(?count) as ?mandatory)
    bind (bound(?def) as ?hasDef)
    bind (bound(?m) as ?array)
    bind (bound(?type) as ?hasType)
    bind (bound(?list) as ?hasEnum)
    optional { ?propShape sh:order ?predefined }
    bind (if(bound(?predefined), ?predefined, "1000"^^xsd:integer) as ?rank)
} order by ?rank

---

prefix st: <http://ns.inria.fr/sparql-template/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :type(?class ?prop) {
    format {
        "<a href=\"%s\"><code>%s</code></a>"
        st:call-template(:href, ?type)
        st:call-template(:label, ?type)
    }
} where {
    ?shape a sh:NodeShape ;
           sh:targetClass ?class ;
           sh:property ?propShape .
    ?propShape sh:path ?prop .
    { ?propShape sh:datatype ?type }
    union
    { ?propShape sh:class ?type }
}

---

prefix sh: <http://www.w3.org/ns/shacl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :enum(?class ?prop) {
    format {
        "%s<code>%s</code>%s"
        if(?first, "one of ", if(?last, "or ", ""))
        ?val
        if(?last, "", ", ")
    }; separator = ""
} where {
    ?shape a sh:NodeShape ;
           sh:targetClass ?class ;
           sh:property ?propShape .
    ?propShape sh:path ?prop ;
               sh:in ?list .
    ?list rdf:rest* ?r .
    ?r rdf:first ?val .
    ?r rdf:rest ?next
    bind ((?list = ?r) as ?first)
    bind ((?next = rdf:nil) as ?last)
}

---

prefix st: <http://ns.inria.fr/sparql-template/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :subclasses(?class) {
    "<p>"
        "The class "
        format { "<code>%s</code>" st:call-template(:label, ?class) }
        " has the following subclasses:"
    "</p>"
    "<ul>"
        st:call-template(:subclass, ?class)
    "</ul>"
} where {
    ?subclass rdfs:subClassOf+ ?class .
}

---

prefix st: <http://ns.inria.fr/sparql-template/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :subclass(?class) {
    "<li>"
        format {
            "<a href=\"%s\"><code>%s</code></a>"
            st:call-template(:href, ?subclass)
            st:call-template(:label, ?subclass)
        }
    "</li>"
} where {
    ?subclass rdfs:subClassOf+ ?class .
    ?subclassShape a sh:NodeShape ;
        sh:targetClass ?subclass .
    ?classShape a sh:NodeShape ;
        sh:targetClass ?class .
    filter (isIRI(?subclass) && isIRI(?class))
}

---

prefix sh: <http://www.w3.org/ns/shacl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :notes(?class) {
    format { "%s" ?note } ; separator = ""
} where {
    ?shape a sh:NodeShape ;
           sh:targetClass ?class ;
           sh:description ?note .
    filter (datatype(?note) = rdf:HTML)
}


---

prefix st: <http://ns.inria.fr/sparql-template/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :href(?term) {
    if(strstarts(str(?term), "http://www.w3.org/2001/XMLSchema#"),
        st:call-template(:href-xsd, ?term),
        st:call-template(:href-fragment, ?term))
} where {}

---

prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :href-xsd(?term) {
    format {
        "http://www.w3.org/TR/2012/REC-xmlschema11-2-20120405/#%s"
        st:call-template(:label, ?term)
    }
} where {}

---

prefix st: <http://ns.inria.fr/sparql-template/>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :href-fragment(?term) {
    format {
        "#%s"
        lcase(st:call-template(:label, ?term))
    }
} where {}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix jsonld: <http://www.w3.org/ns/json-ld#>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :label(?term) {
    format { "%s" ?label }
} where {
    optional {
        ?m a jsonld:Mapping ;
            jsonld:iri ?term ;
            jsonld:term ?name .
    }
    bind (if(bound(?name), ?name, strafter(str(?term), "#")) as ?label)
}

---

prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix : <http://w3c.github.io/wot-thing-description/#>
template :desc(?term) {
    format {
        "%s"
        if(strends(?desc, "."), ?desc, concat(?desc, "."))
    } ; separator = " "
} where {
    ?term rdfs:comment ?desc .
}
